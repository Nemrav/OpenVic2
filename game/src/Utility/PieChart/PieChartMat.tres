[gd_resource type="ShaderMaterial" load_steps=2 format=3 uid="uid://bl2arkbisbk8f"]

[sub_resource type="Shader" id="Shader_af8q7"]
code = "shader_type canvas_item;

//The center in UV coordinates, which will always
//be 0.5, the actual radius will be controlled by the control node
const vec2 center = vec2(0.5,0.5);
const float radius = 0.5;

//Control of the slices
uniform float stopAngles[5];
uniform vec3 colours[5];

//Trim
uniform vec3 trim_colour;
uniform float trim_size = 0.05;

//The center is spotlighted by the gradient,
// control its size and falloff with these
uniform float gradient_falloff = 3.6;
uniform float gradient_base = 3.1;

//control whether this is a donut instead of a pie chart
uniform bool donut = false;
uniform bool donut_inner_trim = false;
uniform float donut_inner_radius = 0.15;

//get the polar coordinates of a pixel relative to the center
vec2 getPolar(vec2 UVin){
	vec2 relcoord = (UVin-center);
	float dist = length(relcoord);
	float theta = PI/2.0 + atan((relcoord.y)/(relcoord.x));
	if(UVin.x < 0.5){
		theta += PI;
	}
	return vec2(dist,theta);
}

//from thebookofshaders, returns a gradient falloff
float parabola( float x, float k ){
    return pow( gradient_base*x*(1.0-x), k );
}

void fragment() {
	vec2 coords = getPolar(UV);
	float dist = coords.x;
	float theta = coords.y;
	//inner hole of the donut => make it trasparent
	if(donut && dist <= donut_inner_radius){
		COLOR = vec4(0.0,0.0,0.0,0.0);
	}
	//inner trim
	else if(donut && donut_inner_trim && dist <= donut_inner_radius + trim_size){
		COLOR = vec4(trim_colour,1.0);
	}
	//interior
	else if(dist <= radius-trim_size){ 
		for(int i=0;i<stopAngles.length();i++){
			if(theta <= stopAngles[i]){
				float gradient = parabola(dist,gradient_falloff);
				COLOR = vec4(colours[i]*(1.0-gradient),1.0);
				break;
			}
		}
	}
	//outer trim
	else if(dist <= radius){ 
		COLOR = vec4(trim_colour,1.0);
	}
	//outside the circle => make it transparent
	else { 
		COLOR = vec4(0.0,0.0,0.0,0.0);
	}
	
}"

[resource]
resource_name = "Piechart_shader"
shader = SubResource("Shader_af8q7")
shader_parameter/stopAngles = [3.14159, 5.02655, 6.28319]
shader_parameter/colours = [Vector3(0, 0, 1), Vector3(1, 1, 0), Vector3(0.4, 0, 0.6)]
shader_parameter/trim_colour = null
shader_parameter/trim_size = 0.021
shader_parameter/gradient_falloff = 3.6
shader_parameter/gradient_base = 3.1
shader_parameter/donut = false
shader_parameter/donut_inner_trim = true
shader_parameter/donut_inner_radius = 0.5
